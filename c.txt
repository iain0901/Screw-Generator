/* 'Nut Job' 螺母、螺栓、墊圈和螺桿生成器
 * 作者：Mike Thompson 1/12/2013, Thingiverse: mike_linus
 *
 * 授權：本作品採用創用CC 姓名標示-非商業性-相同方式分享 3.0 澳洲授權條款
 * 更多資訊請參考 - http://creativecommons.org/licenses/by-nc-sa/3.0/au/deed.en_GB
 *
 * v2 8/12/2013 - 新增套筒頭類型
 * v3 2/11/2014 - 調整蝶形螺母演算法以更好地處理非標準螺母尺寸，並新增 ISO262 公制參考
 * v4 31/12/2014 - 新增套筒頭可選紋理，新增六角頭面數可調整功能，調整特定尺寸蝶形螺母的基準高度
 * v5 11/1/2015 - 新增十字和一字螺絲刀槽，改進紋理處理
 * v6 21/2/2015 - 新增蝶形螺母翼展比例
 * v7 6/3/2016 - 新增擴展選項以控制螺母面數、方形套筒（或任意面數）和套筒深度
 * v8 1/1/2017 - 修改程式碼以移除對已棄用 'assign' 語句的依賴
 * 
 * 本腳本使用修改版的 polyScrewThread.scad 程式庫生成螺母、螺栓、墊圈和螺桿
 * 原始程式庫：polyScrewThead.scad (修改/更新版本 polyScrewThread_r1.scad)
 * http://www.thingiverse.com/thing:8796, CC Public Domain
 *
 * 預設值適用於 8mm 直徑的螺栓、螺桿及相配的螺母和蝶形螺母，可直接使用無需後處理
 * 某些預設參數（如螺母外徑）經過特意調整以產生可手動擰緊的緊密配合
 * 根據不同的印表機誤差、切片工具和線材可能需要調整
 * 建議先列印一組相配的螺栓和螺母進行調整
 * 注意：建議使用低速列印、低溫和實心填充以獲得最佳效果
 */

/* [元件類型] */

type                            = "bolt";//[nut:螺母,bolt:螺栓,rod:螺桿,washer:墊圈]

/* [螺栓和螺桿選項] */

//頭部類型 - 六角、套筒帽、圓頭套筒帽或沉頭套筒帽（螺桿忽略此項）
head_type                       = "hex";//[hex:六角,socket:套筒,button:圓頭,countersunk:沉頭]
//驅動類型 - 套筒、十字或一字（六角頭和螺桿忽略此項）
drive_type                      = "socket";//[socket:套筒,phillips:十字,slot:一字]
//六角頭的對邊距離或套筒/圓頭的直徑（螺桿忽略此項）
head_diameter                   = 12;    
//頭部高度（螺桿忽略此項）
head_height                     = 5;    
//驅動類型直徑（六角頭和螺桿忽略此項）
drive_diameter                  = 5;    
//十字或一字驅動槽的寬度
slot_width                      = 1;
//一字驅動槽的深度
slot_depth                      = 2;
//表面紋理（僅套筒頭）
texture                         = "exclude";//[include:包含,exclude:不包含]
//螺紋外徑
thread_outer_diameter           = 8;        
//螺距（2mm適用於大多數情況，參考ISO262：M3=0.5,M4=0.7,M5=0.8,M6=1,M8=1.25,M10=1.5）
thread_step                     = 2;
//螺紋角度（45度最適合大多數印表機，參考ISO262：30度）
step_shape_degrees              = 45;    
//螺紋段長度
thread_length                   = 25;    
//兩端倒角
countersink                     = 2;    
//無螺紋段長度
non_thread_length               = 0;    
//無螺紋段直徑（-1：與螺紋內徑相同，0：與螺紋外徑相同，其他值：指定值）
non_thread_diameter             = 0;    

/* [螺母選項] */

//類型：普通或蝶形
nut_type                        = "normal";//[normal:普通,wingnut:蝶形]
//六角螺母的對邊距離
nut_diameter                    = 12;    
//螺母高度
nut_height                      = 6;    
//配合螺栓的螺紋外徑（通常設置比螺栓直徑大1mm以便配合 - 根據個人喜好調整）
nut_thread_outer_diameter       = 9;        
//螺距（2mm適用於大多數情況，參考ISO262：M3=0.5,M4=0.7,M5=0.8,M6=1,M8=1.25,M10=1.5）
nut_thread_step                 = 2;
//螺紋角度（45度最適合大多數印表機，參考ISO262：30度）
nut_step_shape_degrees          = 45;    
//翼展比例。蝶形螺母翼展與螺母高度的比例（預設 = 1）
wing_ratio                      = 1;
wing_radius=wing_ratio * nut_height;

/* [墊圈選項] */

//內徑（建議略大於螺栓直徑）
inner_diameter                  = 8;
//外徑
outer_diameter                  = 14;
//厚度
thickness                       = 2;

/* [進階選項] */

//六角頭或螺母的面數。預設為6（標準六角頭和螺母）
facets                         = 6;
//套筒頭孔的面數。預設為6（標準六角套筒）
socket_facets                  = 6;
//套筒頭孔的深度。預設為3.5
socket_depth                   = 3.5;
//解析度（較小的值可獲得更高的解析度，但可能降低渲染速度）
resolution                     = 0.5;    
nut_resolution                 = resolution;

//Hex Bolt
if (type=="bolt" && head_type=="hex")
{
	hex_screw(thread_outer_diameter,thread_step,step_shape_degrees,thread_length,resolution,countersink,head_diameter,head_height,non_thread_length,non_thread_diameter);
}

//Rod
if (type=="rod")
{
	hex_screw(thread_outer_diameter,thread_step,step_shape_degrees,thread_length,resolution,countersink,head_diameter,0,non_thread_length,non_thread_diameter);
}

//Hex Nut (normally slightly larger outer diameter to fit on bolt correctly)
if (type=="nut" && nut_type=="normal")
{
	hex_nut(nut_diameter,nut_height,nut_thread_step,nut_step_shape_degrees,nut_thread_outer_diameter,nut_resolution);
}

//Wing Nut variation of hex nut. Cylinders added to each side of nut for easy turning - ideal for quick release applications
if (type=="nut" && nut_type=="wingnut")
{
	rotate([0,0,30])hex_nut(nut_diameter,nut_height,nut_thread_step,nut_step_shape_degrees,nut_thread_outer_diameter,nut_resolution); //nut	
	translate([(nut_diameter/2)+wing_radius-1,1.5,wing_radius/2+1])rotate([90,0,0])wing(); //attach wing
	mirror(1,0,0)translate([(nut_diameter/2)+wing_radius-1,1.5,wing_radius/2+1])rotate([90,0,0])wing(); //attach wing
}

module wing()
{
	difference()
	{
		cylinder(r=wing_radius,h=3,$fn=64); //cylinder
		union()
		{
			translate([-wing_radius,-wing_radius-1,-0.5])cube([wing_radius*2,wing_radius/2,wing_radius*2]); //remove overhang so flush with base of nut		
			rotate([0,0,90])translate([-wing_radius,wing_radius-1,-0.5])cube([wing_radius*2,wing_radius/2,wing_radius*2]); //remove overhangs so flush with side of nut		
		}
	}
}

//Washer
if (type=="washer")
{
	difference()
	{
		cylinder(r=outer_diameter/2,h=thickness,$fn=100);
		translate([0,0,-0.1])cylinder(r=inner_diameter/2,h=thickness+0.2,$fn=100);
	}
}

//Socket Head Bolt
if (type=="bolt" && head_type!="hex")
{
	socket_screw(thread_outer_diameter,thread_step,step_shape_degrees,thread_length,resolution,countersink,head_diameter,head_height,non_thread_length,non_thread_diameter);
}

module phillips_base()
{
	linear_extrude(slot_width)polygon(points=[[0,0],[(drive_diameter-slot_width)/2,9/5*(drive_diameter-slot_width)/2],[(drive_diameter+slot_width)/2,9/5*(drive_diameter-slot_width)/2],[drive_diameter,0]]);
	translate([(drive_diameter-slot_width)/2,0,(drive_diameter+slot_width)/2])rotate([0,90,0])linear_extrude(slot_width)polygon(points=[[0,0],[(drive_diameter-slot_width)/2,9/5*(drive_diameter-slot_width)/2],[(drive_diameter+slot_width)/2,9/5*(drive_diameter-slot_width)/2],[drive_diameter,0]]);
}

module phillips_fillet()
{
	union()
	{
		translate([-(drive_diameter-slot_width)/2-(slot_width/2),slot_width/2,0])rotate([90,0,0])phillips_base();
		translate([0,0,9/5*(drive_diameter-slot_width)/2])union()
		{
			inner_curve();
			rotate([0,0,90])inner_curve();
			rotate([0,0,180])inner_curve();
			rotate([0,0,270])inner_curve();
		}
	}
}

module inner_curve()
{
	translate([slot_width/2,-slot_width/2,0])rotate([0,90,0])linear_fillet(9/5*(drive_diameter-slot_width)/2,drive_diameter/10);
}

//basic 2d profile used for fillet shape
module profile(radius)
{
  difference()
  {
    square(radius);
    circle(r=radius);
  }
}

//linear fillet for use along straight edges
module linear_fillet(length,profile_radius)
{
	translate([0,-profile_radius,profile_radius])rotate([0,90,0])linear_extrude(height=length,convexity=10)profile(profile_radius);
}

module phillips_drive()
{
	intersection()
	{
		phillips_fillet();
		cylinder(9/5*(drive_diameter-slot_width)/2,drive_diameter/2+(slot_width/2),slot_width/2);
	}
}

module socket_screw(od,st,lf0,lt,rs,cs,df,hg,ntl,ntd)
{
    ntr=od/2-(st/2)*cos(lf0)/sin(lf0);
	$fn=60;

	difference()
	{
    		union()
    		{
        		if (head_type=="socket")
				{
					socket_head(hg,df);
				}
			
        		if (head_type=="button")
				{
					button_head(hg,df);				
				}

        		if (head_type=="countersunk")
				{
					countersunk_head(hg,df);				
				}

        		translate([0,0,hg])
        		if ( ntl == 0 )
        		{
          		cylinder(h=0.01, r=ntr, center=true);
        		}
        		else
        		{
            		if ( ntd == -1 )
            		{
                		cylinder(h=ntl+0.01, r=ntr, $fn=floor(od*PI/rs), center=false);
            		}
            		else if ( ntd == 0 )
            		{
                		union()
                		{
                    		cylinder(h=ntl-st/2,r=od/2, $fn=floor(od*PI/rs), center=false);
                    		translate([0,0,ntl-st/2])
                    		cylinder(h=st/2,
                             r1=od/2, r2=ntr, 
                             $fn=floor(od*PI/rs), center=false);
                		}
            		}
            		else
            		{
                		cylinder(h=ntl, r=ntd/2, $fn=ntd*PI/rs, center=false);
            		}
        		}
        		translate([0,0,ntl+hg]) screw_thread(od,st,lf0,lt,rs,cs);
    		}
		//create opening for specific drive type
		if (drive_type=="socket")
		{
			cylinder(r=drive_diameter/2,h=socket_depth,$fn=socket_facets); //socket
			translate([0,0,socket_depth])cylinder(r1=drive_diameter/2,r2=0,h=drive_diameter/3,$fn=socket_facets); //socket tapers at base to allow printing without bridging and improve socket grip
		}
		else
		{
			if (drive_type=="phillips")
			{
				translate([0,0,-0.001])phillips_drive();
			}
			else //slot
			{
				translate([-(drive_diameter)/2,slot_width/2,-0.001])rotate([90,0,0])cube([drive_diameter,slot_depth,slot_width]);
			}	
		}
	}
}

module socket_head(hg,df)
{
	texture_points=2*PI*(head_diameter/2);
	texture_offset=head_diameter/18;
	texture_radius=head_diameter/24;

	rd0=df/2/sin(60);
	x0=0;	x1=df/2;	x2=x1+hg/2;
	y0=0;	y1=hg/2;	y2=hg;

	intersection()
	{
	   	cylinder(h=hg, r=rd0, $fn=60, center=false);
		rotate_extrude(convexity=10, $fn=6*round(df*PI/6/0.5))
		polygon([ [x0,y0],[x1,y0],[x2,y1],[x1,y2],[x0,y2] ]);
	}

	if (texture=="include") //add texture to socket head. Adjust texture density and size using texture variables above 
	{
		for (i= [1:texture_points])
		{
			translate([cos(360/texture_points*i)*(head_diameter/2+texture_offset), sin(360/texture_points*i)*(head_diameter/2+texture_offset), 1 ])
			rotate([0,0,360/texture_points*i])cylinder(r=texture_radius,h=head_height*0.6,$fn=3);
		}
	}

}

module button_head(hg,df)
{
	rd0=df/2/sin(60);
	x0=0;	x1=df/2;	x2=x1+hg/2;
	y0=0;	y1=hg/2;	y2=hg;

	intersection()
	{
	   	cylinder(h=hg, r1=drive_diameter/2 + 1, r2=rd0, $fn=60, center=false);
		rotate_extrude(convexity=10, $fn=6*round(df*PI/6/0.5))
		polygon([ [x0,y0],[x1,y0],[x2,y1],[x1,y2],[x0,y2] ]);
	}
}

module countersunk_head(hg,df)
{
	rd0=df/2/sin(60);
	x0=0;	x1=df/2;	x2=x1+hg/2;
	y0=0;	y1=hg/2;	y2=hg;

	intersection()
	{
	   cylinder(h=hg, r1=rd0, r2=thread_outer_diameter/2-0.5, $fn=60, center=false);

		rotate_extrude(convexity=10, $fn=6*round(df*PI/6/0.5))
		polygon([ [x0,y0],[x1,y0],[x2,y1],[x1,y2],[x0,y2] ]);
	}
}

/* 程式庫包含在下方以支援客製化功能    
 *   
 * polyScrewThread_r1.scad    作者：aubenc @ Thingiverse
 *
 * 由 mike_mattala @ Thingiverse 於 1/1/2017 修改以移除已棄用的 assign
 * 
 * 此腳本包含可用於生成螺紋桿、螺絲和螺母的程式庫模組
 *
 * http://www.thingiverse.com/thing:8796
 *
 * CC Public Domain
 */

module screw_thread(od,st,lf0,lt,rs,cs)
{
    or=od/2;
    ir=or-st/2*cos(lf0)/sin(lf0);
    pf=2*PI*or;
    sn=floor(pf/rs);
    lfxy=360/sn;
    ttn=round(lt/st+1);
    zt=st/sn;

    intersection()
    {
        if (cs >= -1)
        {
           thread_shape(cs,lt,or,ir,sn,st);
        }

        full_thread(ttn,st,sn,zt,lfxy,or,ir);
    }
}

module hex_nut(df,hg,sth,clf,cod,crs)
{

    difference()
    {
        hex_head(hg,df);

        hex_countersink_ends(sth/2,cod,clf,crs,hg);

        screw_thread(cod,sth,clf,hg,crs,-2);
    }
}


module hex_screw(od,st,lf0,lt,rs,cs,df,hg,ntl,ntd)
{
    ntr=od/2-(st/2)*cos(lf0)/sin(lf0);

    union()
    {
        hex_head(hg,df);

        translate([0,0,hg])
        if ( ntl == 0 )
        {
            cylinder(h=0.01, r=ntr, center=true);
        }
        else
        {
            if ( ntd == -1 )
            {
                cylinder(h=ntl+0.01, r=ntr, $fn=floor(od*PI/rs), center=false);
            }
            else if ( ntd == 0 )
            {
                union()
                {
                    cylinder(h=ntl-st/2,
                             r=od/2, $fn=floor(od*PI/rs), center=false);

                    translate([0,0,ntl-st/2])
                    cylinder(h=st/2,
                             r1=od/2, r2=ntr, 
                             $fn=floor(od*PI/rs), center=false);
                }
            }
            else
            {
                cylinder(h=ntl, r=ntd/2, $fn=ntd*PI/rs, center=false);
            }
        }

        translate([0,0,ntl+hg]) screw_thread(od,st,lf0,lt,rs,cs);
    }
}

module hex_screw_0(od,st,lf0,lt,rs,cs,df,hg,ntl,ntd)
{
    ntr=od/2-(st/2)*cos(lf0)/sin(lf0);

    union()
    {
        hex_head_0(hg,df);

        translate([0,0,hg])
        if ( ntl == 0 )
        {
            cylinder(h=0.01, r=ntr, center=true);
        }
        else
        {
            if ( ntd == -1 )
            {
                cylinder(h=ntl+0.01, r=ntr, $fn=floor(od*PI/rs), center=false);
            }
            else if ( ntd == 0 )
            {
                union()
                {
                    cylinder(h=ntl-st/2,
                             r=od/2, $fn=floor(od*PI/rs), center=false);

                    translate([0,0,ntl-st/2])
                    cylinder(h=st/2,
                             r1=od/2, r2=ntr, 
                             $fn=floor(od*PI/rs), center=false);
                }
            }
            else
            {
                cylinder(h=ntl, r=ntd/2, $fn=ntd*PI/rs, center=false);
            }
        }

        translate([0,0,ntl+hg]) screw_thread(od,st,lf0,lt,rs,cs);
    }
}

module thread_shape(cs,lt,or,ir,sn,st)
{
    if ( cs == 0 )
    {
        cylinder(h=lt, r=or, $fn=sn, center=false);
    }
    else
    {
        union()
        {
            translate([0,0,st/2])
              cylinder(h=lt-st+0.005, r=or, $fn=sn, center=false);

            if ( cs == -1 || cs == 2 )
            {
                cylinder(h=st/2, r1=ir, r2=or, $fn=sn, center=false);
            }
            else
            {
                cylinder(h=st/2, r=or, $fn=sn, center=false);
            }

            translate([0,0,lt-st/2])
            if ( cs == 1 || cs == 2 )
            {
                  cylinder(h=st/2, r1=or, r2=ir, $fn=sn, center=false);
            }
            else
            {
                cylinder(h=st/2, r=or, $fn=sn, center=false);
            }
        }
    }
}

module full_thread(ttn,st,sn,zt,lfxy,or,ir)
{
  if(ir >= 0.2)
  {
    for(i=[0:ttn-1])
    {
        for(j=[0:sn-1])
        {
			pt = [[0,0,i*st-st],
                 [ir*cos(j*lfxy),     ir*sin(j*lfxy),     i*st+j*zt-st       ],
                 [ir*cos((j+1)*lfxy), ir*sin((j+1)*lfxy), i*st+(j+1)*zt-st   ],
				 [0,0,i*st],
                 [or*cos(j*lfxy),     or*sin(j*lfxy),     i*st+j*zt-st/2     ],
                 [or*cos((j+1)*lfxy), or*sin((j+1)*lfxy), i*st+(j+1)*zt-st/2 ],
                 [ir*cos(j*lfxy),     ir*sin(j*lfxy),     i*st+j*zt          ],
                 [ir*cos((j+1)*lfxy), ir*sin((j+1)*lfxy), i*st+(j+1)*zt      ],
                 [0,0,i*st+st]];
               
            polyhedron(points=pt,faces=[[1,0,3],[1,3,6],[6,3,8],[1,6,4], //changed triangles to faces (to be deprecated)
										[0,1,2],[1,4,2],[2,4,5],[5,4,6],[5,6,7],[7,6,8],
										[7,8,3],[0,2,3],[3,2,7],[7,2,5]	]);
        }
    }
  }
  else
  {
    echo("Step Degrees too agresive, the thread will not be made!!");
    echo("Try to increase de value for the degrees and/or...");
    echo(" decrease the pitch value and/or...");
    echo(" increase the outer diameter value.");
  }
}

module hex_head(hg,df)
{
	rd0=df/2/sin(60);
	x0=0;	x1=df/2;	x2=x1+hg/2;
	y0=0;	y1=hg/2;	y2=hg;

	intersection()
	{
	   cylinder(h=hg, r=rd0, $fn=facets, center=false);

		rotate_extrude(convexity=10, $fn=6*round(df*PI/6/0.5))
		polygon([ [x0,y0],[x1,y0],[x2,y1],[x1,y2],[x0,y2] ]);
	}
}

module hex_head_0(hg,df)
{
    cylinder(h=hg, r=df/2/sin(60), $fn=6, center=false);
}

module hex_countersink_ends(chg,cod,clf,crs,hg)
{
    translate([0,0,-0.1])
    cylinder(h=chg+0.01, 
             r1=cod/2, 
             r2=cod/2-(chg+0.1)*cos(clf)/sin(clf),
             $fn=floor(cod*PI/crs), center=false);

    translate([0,0,hg-chg+0.1])
    cylinder(h=chg+0.01, 
             r1=cod/2-(chg+0.1)*cos(clf)/sin(clf),
             r2=cod/2, 
             $fn=floor(cod*PI/crs), center=false);
}